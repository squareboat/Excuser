apply plugin: 'com.android.application'

// Load keystore.properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    // Allows you to reference product flavors in your
    // phone module's build.gradle file
    publishNonDefault true

    defaultConfig {
        applicationId "com.squareboat.excuser"
        minSdkVersion 23
        targetSdkVersion versions.targetSdk
        versionCode = Integer.valueOf("" + appVersionCode).intValue()
        versionName "${appVersionName}"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['STORE_FILE'])
            keyAlias keystoreProperties['KEY_ALIAS']
            storePassword keystoreProperties['STORE_PASSWORD']
            keyPassword keystoreProperties['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        wear1 {
            // Use the defaultConfig value
        }
        wear2 {
            minSdkVersion 25
            versionCode = Integer.valueOf("" + wearAppVersionCode).intValue()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation ("com.android.support:wear:$versions.supportLibrary")
    compileOnly "com.google.android.wearable:wearable:$versions.wearable"
    implementation "com.google.android.support:wearable:$versions.wearable"
    implementation "com.google.android.gms:play-services-wearable:$versions.playServices"
}
