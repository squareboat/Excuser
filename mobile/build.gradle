buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

// Load keystore.properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "com.squareboat.excuser"
        minSdkVersion 21
        targetSdkVersion versions.targetSdk
        versionCode = Integer.valueOf("" + appVersionCode).intValue()
        versionName "${appVersionName}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        wearAppUnbundled true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['STORE_FILE'])
            keyAlias keystoreProperties['KEY_ALIAS']
            storePassword keystoreProperties['STORE_PASSWORD']
            keyPassword keystoreProperties['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            // Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
            ext.alwaysUpdateBuildId = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        cruncherEnabled = false
    }

    dexOptions {
        // Sets the maximum number of DEX processes
        // that can be started concurrently.
        maxProcessCount 8
        // Sets the maximum memory allocation pool size
        // for the dex operation.
        javaMaxHeapSize "2g"
        // Enables Gradle to pre-dex library dependencies.
        preDexLibraries true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    wearApp project(path: ':wear', configuration: 'wear1Release')
    implementation "com.google.android.gms:play-services-wearable:$versions.playServices"
    compileOnly "com.google.android.wearable:wearable:$versions.wearable"
    implementation "com.google.android.support:wearable:$versions.wearable"

    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.android.support:design:$versions.supportLibrary"
    implementation "com.android.support:cardview-v7:$versions.supportLibrary"
    implementation "com.android.support:preference-v7:$versions.supportLibrary"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    implementation "net.frakbot:glowpadbackport:2.1.1"
    implementation("com.afollestad.material-dialogs:core:0.9.4.5") {
        exclude group: "com.android.support"
    }
    implementation "com.google.code.gson:gson:2.8.2"
    testImplementation "junit:junit:4.12"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    implementation "com.google.firebase:firebase-core:$versions.playServices"
}

apply plugin: 'com.google.gms.google-services'